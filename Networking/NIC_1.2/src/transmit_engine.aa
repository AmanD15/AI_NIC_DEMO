//	Pulls packet(to be transmitted) from memory
//	and pushes it to MAC
//
//	step 0 : Wait until enabled
//	step 1 : Get Tx buffer pointer from TxQ register and pop it from TxQ
//	step 2 : Transmit (read) packet data from the acquired Tx buffer
//	step 3 : Write the buffer pointer into the freeQ pointed by freeQ register 
//	Perform sequentilly for all servers so that a particular server is not overburdened

$module [TransmitEngineDaemon] $in () $out () $is
{
	// The last index in the transmit queue which was read from
	LAST_READ_TX_QUEUE_INDEX := 0
	
	$branchblock[main]
	{
		// spin until enabled by software
		$merge $entry not_enabled_yet_loopback disable_loopback $endmerge
		
		// If not enabled yet -> Loopback
		$if (~(S_CONTROL_REGISTER [] 0)) $then
			$place [not_enabled_yet_loopback]
		$endif

		tx_tag := ($bitcast ($uint<8>) P_TX_ENGINE_TAG)
		$rreport(TransmitEngineDaemon got_control)
	
		$dopipeline $depth 31 $fullrate	
			$merge $entry $loopback 
				$phi TxQ_server_id := (LAST_READ_TX_QUEUE_INDEX & ($bitcast ($uint<8>) (S_NUMBER_OF_SERVERS - 1)))
							$on $entry n_TxQ_server_id $on $loopback
			$endmerge
				
			$rreport(TransmitEngineDaemon trying_to_get_pointer_from_tx_queue TxQ_server_id)

			// Get packet pointer for server TxQ_server_id
			$call getTxPacketPointer (tx_tag TxQ_server_id) (tx_buffer_pointer tx_flag)
			// tx_flag == 0 is success, i.e., popped packet pointer from TxQ
			$barrier
			//
			// TX_ACTIVITY_LOGGER := 1
			//
				
			// if ok transmit the packet 
			$guard (~tx_flag) $call transmitPacket (tx_tag tx_buffer_pointer) (transmitted_flag)
			// transmitted_flag == 1, then packet is transmitted successfully
			$barrier
			//
			// TX_ACTIVITY_LOGGER := 2
			//

			
			$volatile push_pointer_back_to_free_Q := ((~tx_flag) & transmitted_flag)


			// push a packet pointer to free queue
			$rreport(TransmitEngineDaemon pushing_back_to_free_queue push_pointer_back_to_free_Q tx_buffer_pointer)
			
			$guard (push_pointer_back_to_free_Q)
				$call pushIntoQueue (tx_tag FREEQUEUE TxQ_server_id tx_buffer_pointer) (push_status)
			// push_status == 0 is success, i.e., pushed packet pointer back to freeQ
			$barrier
			//
			// TX_ACTIVITY_LOGGER := 3
			// 

			// Increment the number of packets transmitted
			$guard (push_pointer_back_to_free_Q)
				$call incrementNumberOfPacketsTransmitted () ()

			// move to next server's Tx queue
			n_TxQ_server_id := ((TxQ_server_id + 1) & ($bitcast ($uint<8>) (S_NUMBER_OF_SERVERS - 1)))
			LAST_READ_TX_QUEUE_INDEX := TxQ_server_id

			$barrier
			//
			// TX_ACTIVITY_LOGGER := 4
			//
	
			// while (control[0] == 1) loop is enabled
			$while (S_CONTROL_REGISTER [] 0)
			
		$place [disable_loopback]
	}
}


//	Get a transmit packet buffer from server TxQ_server_id
$pipeline $depth 7
$module [getTxPacketPointer] 
	$in (tag :$uint<8> TxQ_server_id : $uint<8>)
	$out (tx_buffer_pointer : $uint<64> status : $uint<1>) $is
{
	$call popFromQueue (tag TXQUEUE TxQ_server_id) (tx_buffer_pointer status)

	$attribute input_buffering 0
}



// 	Transmit packet, given the pointer to the packet
// 	and return 1 if success.
$module [transmitPacket]
	$in (tag :$uint<8> packet_pointer: $uint<64>) 
	$out (status: $uint<1>) $is
{
	// read packet information from transmit packet buffer.
	$volatile control_data_addr := packet_pointer

	$rreport(transmitPacket control_data_addr_is packet_pointer control_data_addr)	
	$call accessMemoryDword (tag READMEM control_data_addr  0 $zero<64>) (control_data)
	
	
	// get packet_size and tkeep(bytemask) of last word
	$volatile $split (control_data 45 11 8 ) (unused packet_size last_tkeep)
	$rreport(transmitPacket control_data_received_is packet_size last_tkeep)	
	$branchblock[loop]
	{
		$dopipeline $depth 31 $fullrate
		$merge $entry $loopback

			$phi count_down := (packet_size - 16) $on $entry ncount_down $on $loopback

			//packet starts after 3 d-words (64 bits each) in the buffer
			$phi mem_addr_offset := ($bitcast ($uint<16>) 24) $on $entry nmem_addr_offset $on $loopback

		$endmerge

		// read packet data
		$call accessMemoryDword (tag READMEM control_data_addr 
							($bitcast ($uint<64>) mem_addr_offset) $zero<64>) (data)
 
		// transmit packet to MAC.
		nic_to_mac_transmit_pipe := ($concat $zero<1> data FULL_BYTE_MASK)

		// compute next count down and next memory address.
		$volatile  ncount_down := (count_down - 8)
		$volatile  nmem_addr_offset := (mem_addr_offset + 8)
		
		$volatile not_last_word := (ncount_down > 8)

		// loop till last word
		$while not_last_word
	

		last_offset := nmem_addr_offset
	
		//last word
		$call accessMemoryDword (tag READMEM control_data_addr 
						($bitcast ($uint<64>) last_offset) $zero<64>) (last_word)
		
		// transmit last word
		$rreport(transmitPacket sending_last_word last_word)
		nic_to_mac_transmit_pipe := ($concat $one<1> last_word last_tkeep)
		
		// transmit successful if packet_size amount of data is succesfully transmitted
		status := (packet_size == ($bitcast ($uint<11>) last_offset))
	}	
}
